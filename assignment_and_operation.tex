\chapter{赋值与运算}

\section{赋值}\label{fortran_assignment}

赋值\footnote{
    实际上按标准来说, 赋值有两种: 固有赋值(intrinsic assignment)和超载赋值(defined assignment). 本章``赋值''仅指固有赋值.
}(assignment)是将一个数据实体的值拷贝给一个变量的操作.

用形如\texttt{a = b}的语句即可将\texttt{b}中的内容拷贝给\texttt{a}. 请看下面的例子.
\begin{lstlisting}
program main

    implicit none
    integer :: a, b

    ! Assign 1 to a.
    a = 1
    ! Now "print *, a" will return 1.

    ! Assign a to b.
    b = a
    ! Now "print *, b" will return 1.

end program main
\end{lstlisting}

Fortran是强\footnote{
    事实上``强''和``弱''的定义是模糊的, 不过一般都将Fortran归为强类型语言.
}类型语言(strongly typed language), 这意味着Fortran在赋值时``不允许''类型转化, 数字型数据实体只能赋给数字型变量, 字符型数据实体只能赋给字符型变量, 逻辑型数据实体只能赋给逻辑型变量.

官方规则是这么说, 实际却不完全是这样. Ifx和Gfortran在\uline{不同程度}上是允许数字型数据实体和逻辑型数据实体相互赋值的, 赋值时遵循的规则还不太一样. 我懒得总结Ifx和Gfortran在这方面的``器规''了, 同志们自己试试, 自己总结.

\begin{convention}
    即使编译器允许, 也不要跨类型赋值.
\end{convention}

赋值的时候, ``\texttt{=}''两边的类型或种别可能不一样, 这时自然有个类型和种别转化的问题. Fortran是静态类型语言, 所以转化的总则必然是: 把``\texttt{=}''右边的数据实体转化成类型和种别与``\texttt{=}''左边变量相同的数据实体, 然后赋值.

任何一个Fortran数字型数据实体都对应于一个数. 要细说来, 数字型数据实体赋值时的转化规则是非常复杂的, 我也说不清楚. 但基本原则是确定的, 就是``尽量''让转化前后的数据实体对应的数接近. 如果是``精度低''的数据实体转化成``精度高''的数据实体, 只要能转化, 那么转化前后的数据实体对应的数就是相等的. 比如整型转实型\footnote{
    一般情形下整型转实型都是``无损''的, 但有些极端情形不是这样. 幸好这些极端情形是很难碰上的.
}, 实型转复型, 低精度种别转高精度种别, 都是这样, 这时我们就可以放心大胆地赋值, 反正赋值后数据实体的实际意义不变.

如果是``精度高''的数据实体转化成``精度低''的数据实体, 事情就比较复杂.

如果是实型转整型, 我也不清楚标准规则怎么说, 但Ifx和Gfortran都是``向0取整''. 可以运行下面这个程序来感受一下.
\begin{lstlisting}
program main
    implicit none
    integer :: i
    i = +9.87654321
    print *, i
    i = -9.87654321
    print *, i
end program main
\end{lstlisting}

如果是复型转实型, 自然是取实部.

如果是复型转整型, 则是先转实型, 再转整型.

还有很多具体情况(比如同类型``高精度''种别向``低精度''种别转化)的规则我也不清楚, 但实型和复型数据实体都是比较精确的, ``\texttt{=}''左右的数据实体, 对应的数非常接近, 一般其中的区别可以不用管.

总而言之, 数字型数据实体赋值时的转化规则是非常复杂, 但一般情况下可以不管这些规则. 原因有以下三点:
\begin{itemize}
    \item 除了复型转实型, 复型转整型, 和实型转整型, 转换前后有较大的区别外, 其他情况下转换前后的区别通常可以忽略;
    \item 复型转实型, 复型转整型, 和实型转整型的规则是比较明确的;
    \item (最重要的一点)通常在计算时都是使用同种别的实型(和复型), 基本没有类型转化的时候(这时候就根本不需要去记这些个复杂的规则啦).
\end{itemize}

统一使用同种别的实型(和复型), 能防止很多意料之外的情况出现.

\begin{convention}\label{fortran_use_real}
    尽量使用同种别的实型$(\text{和复型})$常量和变量.
\end{convention}

字符串赋值时还有个长度匹配的问题. 如果``\texttt{=}''左边字符串的长度小于右边字符串的长度, 则把右边字符串尾巴多出的部分砍掉, 然后赋值. 如果``\texttt{=}''左边字符串的长度大于右边字符串的长度, 则在右边字符串尾巴处补上空格让长度一样, 然后赋值.
\begin{lstlisting}
program main
    implicit none
    character(4) :: sc
    character(5) :: nc
    character(6) :: lc
    sc = 'hello'
    print *, '"', sc, '"'
    nc = 'hello'
    print *, '"', nc, '"'
    lc = 'hello'
    print *, '"', lc, '"'
end program main
\end{lstlisting}
我们还可以给字符串的切片赋值, 这样只会修改切片的那部分.
\begin{lstlisting}
program main
    implicit none
    character(13) :: hello_world
    hello_world = 'hello, world!'
    print *, hello_world
    hello_world(1:1) = 'H'
    print *, hello_world
end program main
\end{lstlisting}

\section{运算}\label{fortran_opration}

运算\footnote{
    实际上按标准来说, 运算有两种: 固有运算(intrinsic operation)和超载运算(defined operation). 本章``运算''仅指固有运算.
}(operation)是由一个或两个数据实体得到另一个数据实体的操作, 其中很多是与数学中的运算是对应的. 运算是要使用运算符(operator)的, 运算符分四类: 算数运算符(numeric operator), 字符运算符(character operator), 逻辑运算符(logical operator), 关系运算符(relational operator).

如果一个运算符将一个数据实体变为另一个数据实体, 则其一定要按\texttt{[op][q]}的方式使用, 其中\texttt{[op]}是运算符, \texttt{[q]}是一个数据实体. 如果一个运算符将两个数据实体变为另一个数据实体, 则其一定要按\texttt{[q1][op][q2]}的方式使用, 其中\texttt{[op]}是运算符, \texttt{[q1]}和\texttt{[q2]}是两个数据实体. 

形如\texttt{[op][q]}或\texttt{[q1][op][q2]}的东东称为表达式(expression), 表达式本身也是数据实体, 这就形成一个套娃定义.

形如\texttt{[q1][op12][q2][op23][q3]}的东东, 按套娃定义, 这也是一个表达式. 问题是这个表达式有两种解释.
\begin{enumerate}
    \item 先把\texttt{[q1][op12][q2]}算出来, 得到一个数据实体\texttt{[q12]},\\则\texttt{[q1][op12][q2][op23][q3]}等同于\texttt{[q12][op23][q3]}.
    \item 先把\texttt{[q2][op23][q3]}算出来, 得到一个数据实体\texttt{[q23]},\\则\texttt{[q1][op12][q2][op23][q3]}等同于\texttt{[q1][op12][q23]}.
\end{enumerate}
按两种解释方式分别计算, 最后得到的结果可能不同, 怎么办? 数学中的运算符可能有优先级, 比如乘除优先于加减. Fortran中的任何一个运算符都有优先级, 对应地, Fortran中的运算有两个规则:
\begin{enumerate}
    \item 若\texttt{[op12]}的优先级不低于\texttt{[op23]},\\则对于表达式\texttt{[q1][op12][q2][op23][q3]}, 先计算\texttt{[q1][op12][q2]}.\footnote{
        有特例, 见\ref{fortran_numeric_operator}.
    }
    \item 若\texttt{[op12]}的优先级低于\texttt{[op23]},\\则对于表达式\texttt{[q1][op12][q2][op23][q3]}, 先计算\texttt{[q2][op23][q3]}.
\end{enumerate}
这和数学中的运算是完全对应的.

可以加\texttt{()}, 来强制让\texttt{()}里的部分先算, 这和数学也是对应的. 需要注意的是, 不能用\texttt{[]}和\texttt{{}}, 统统用\texttt{()}代替.

\subsection{算数运算}\label{fortran_numeric_operator}

算数运算符一共五个: \texttt{+}, \texttt{-}, \texttt{*}, \texttt{/}, \texttt{**}, 分别对应于加, 减, 乘, 除, 乘方.
\begin{lstlisting}
program main
    implicit none
    real :: a, b
    a = 3.0
    b = 4.0
    print *, a+b ! Add 3.0 and 4.0.
    print *, a-b ! Subtract 3.0 from 4.0.
    print *, a*b ! Multiply 3.0 by 4.0.
    print *, a/b ! Divide 3.0 by 4.0.
    print *, a**b ! Raise 3.0 to the power 4.0.
end program main
\end{lstlisting}

开方怎么算? 自己想!

五个算数运算符的优先级和数学中是一样的.
\begin{lstlisting}
program main
    implicit none
    print *, 1.0+2.0*3.0 ! Result is 7.0.
    print *, (1.0+2.0)*3.0 ! Result is 9.0.
    print *, 4.0*5.0**6.0 ! Result is 6.25e4.
    print *, (4.0*5.0)**6.0 ! Result is 6.4e7.
end program main
\end{lstlisting}

严格上来说, Fortran有七个算数运算符, 另两个也是``\texttt{+}''和``\texttt{-}''. 众所周知, 数学中``$+$''和``$-$''前面可以没有数. Fortran中也一样. 对于任意一个数据实体\texttt{[q]}, \texttt{+[q1]}/\texttt{-[q1]}就相当于\texttt{[zero]+[q1]}/\texttt{[zero]-[q1]}, 其中\texttt{[zero]}是精度最低的种别的\texttt{0}.

看下面这个例子就懂了.
\begin{lstlisting}
program main
    implicit none
    real :: a
    a = 1.0
    print *, -a**2.0 ! Result is -1.0.
    print *, (-a)**2.0 ! Result is 1.0.
end program main
\end{lstlisting}

数学中, $-a^2$也是表示$-(a^2)$哟!

运算符两边的类型相同时, 结果就是那个类型. 运算符两边的类型和种别都相同时, 结果就是那个类型那个种别. 运算符两边的类型或种别不同时, 又遇到类型转化或种别转化的问题了. 转化的基本规则就是两个数据实体中哪个更精确, 结果就转化成那个数据实体的类型和种别. 比如上面的程序, 把\texttt{2.0}改成\texttt{2}, 结果不变. 因为无论是\texttt{a}还是\texttt{-a}, 都是实型, 肯定比整型的\texttt{2}精确, 所以结果就是实型的\texttt{-1.0}和\texttt{1.0}.

表达式既然是数据实体, 自然可以赋值给变量. 注意赋值时还要遵循赋值的类型和种别转化规则.

一般情况下都不需理会运算时的类型转化, 毕竟转化前后数学上差别不大. 但这里有一个非常传统, 非常典型, 非常折磨人的大坑, 巨坑, 奆坑. 这个坑就是整型数据实体相除.

来看下面这个例子.
\begin{lstlisting}
program main
    implicit none
    print *, 2**(1/4)
end program main
\end{lstlisting}

乍一看, 这个程序非常聪明地将$\sqrt[4]{2}$算了出来, 然而一运行, 就会发现不对. 为什么? 让我们来一点一点分析电脑是怎么干活的.

首先, 电脑先算\texttt{1/4}. $1/4=0.25$, 这没什么问题. 然而, 根据刚才说的类型转化规则, \texttt{1/4}的结果必须转化成整型. 转化后什么结果? 和赋值时一样的规则, 向0取整, 结果是\texttt{0}. 所以\texttt{1/4}就是\texttt{0}!

然后电脑再算\texttt{2**0}, 结果自然是\texttt{1}.

也就是说, 将两个整型数据实体相``除'', 实际上是将它们整除!

再来看下面这个程序.

\begin{lstlisting}
program main
    implicit none
    integer :: p, q
    real :: r
    p = 1.0
    q = 4.0
    r = p/q
    print *, 2.0**r
end program main
\end{lstlisting}
这个程序的结果也不大对. 让我们再来分析分析这个程序.

首先给\texttt{p}和\texttt{q}分别赋上\texttt{1.0}和\texttt{4.0}. 然而, \texttt{p}和\texttt{q}都是整型的. 根据赋值时的类型转化规则, \texttt{p}和\texttt{q}分别是\texttt{1}和\texttt{4}.

然后计算\texttt{p/q}并赋给\texttt{r}. \texttt{p}是\texttt{1}, \texttt{q}是\texttt{4}, $1/4=0.25$. 然而\texttt{p}和\texttt{q}都是整型, 所以结果是\texttt{0}. 虽然\texttt{r}是实型, 然而并没有什么卵用. 因为是先计算, 再赋值, 所以计算时会先进行一次类型转化, 转化完了赋值时再进行一次类型转化. 先计算\texttt{p/q}, 类型转化后得到\texttt{0}, 然后赋给\texttt{r}, 再类型转化, 所以\texttt{r}是\texttt{0.0}.

最后计算\texttt{2.0**r}, 结果自然是\texttt{1.0}.

这个坑非常经典, 好多程序语言都有, 就连Python2默认都会如此\dots

有一个非常简单的方法能避开上面所说的坑, 就是遵守规范\ref{fortran_use_real}, 也就是: 统统用实型. 这可真是血泪教训啊!

最后还有一个小坑, 就是乘方从右边先算(非常特殊). 比如\texttt{a**b**c}, 到底是${a^b}^c$, 还是$a^{b^c}$? 答案是$a^{b^c}$, 也就是先算$b^c$.可以这么记: \texttt{(a**b)**c}其实等于\texttt{a**(b*c)}, 如果\texttt{a**b**c}是\texttt{(a**b)**c}, 就显得太傻了, 还不如直接写\texttt{a**(b*c)}\footnote{
    乘法应该比乘方快\dots
}, 所以\texttt{a**b**c}应该是\texttt{a**(b**c)}.
\begin{lstlisting}

program main
    implicit none
    real :: a, b, c
    a = 4.0
    b = 3.0
    c = 2.0
    print *, a**b**c ! This is a**(b**c).
end program main
\end{lstlisting}

只要记得这里有个坑就好了. 真遇到连着两个\texttt{**}, 不清楚哪个先算, 加个括号呀. 即使自己清楚, 也最好加个括号, 一是让自己更清楚, 二是照顾照顾别人(造轮子的时候), 因为别人很可能忘了到底哪个\texttt{**}先算.

\begin{convention}\label{use_barket}
    在表达式中适当地多用括号以保证表达式能被轻松理解.
\end{convention}

还有一个小细节. 严格意义上说, \texttt{+}和\texttt{-}前面直接跟运算符是非法的. 然而器有器规呀, Gfortran只是送个警告, Ifx直接放行. 不过还是多加一个括号吧. 可以运行运行下面这个程序来看看结果.
\begin{lstlisting}
program main
    implicit none
    real :: a, b
    a = 3.0
    b = 4.0
    print *, a+-b ! It is proper to write a+-b as a+(-b).
    print *, a--b ! It is proper to write a--b as a-(-b).
    print *, a*-b ! It is proper to write a*-b as a*(-b).
    print *, a/-b ! It is proper to write a/-b as a/(-b).
    print *, a**-b ! It is proper to write a**-b as a**(-b).
end program main
\end{lstlisting}

\subsection{字符运算}

字符运算符只有一个: \texttt{//}, 其作用是把左右两边的字符串连起来得到一个新字符串. 来看下面这个例子.
\begin{lstlisting}

program main
    implicit none
    print *, 'Hello'//','//' '//'world'//'!'
end program main
\end{lstlisting}

如果把连接后得到的字符串赋给变量, 也要遵循字符串赋值的长度转换规则. 比如, 下面这个程序运行后``什么也没输出''\footnote{
    请自己思考为什么这里加了引号. 可以另造个真什么也没干的程序, 运行看看有什么区别.
}.
\begin{lstlisting}
program main
    implicit none
    character(0) :: null_char
    null_char = 'Hello'//','//' '//'world'//'!'
    print *, null_char
end program main
\end{lstlisting}

\subsection{逻辑运算}

逻辑运算符一共有五个. 常用的有三个: \texttt{.and.}, \texttt{.or.}, \texttt{.not.}, 分别代表与, 或, 非. 不常用的有三个: \texttt{.eqv.}和\texttt{.neqv.}, 分别代表同或和异或. 一般用真值表来展现逻辑运算的结果. 下面的程序会输出完整的真值表.
\begin{lstlisting}
program main
    implicit none
    print *, '.and.'
    print *, ' ', .true., .false.
    print *, .true., .true..and..true., .true..and..false.
    print *, .false., .false..and..true., .false..and..false.
    print *, '.or.'
    print *, ' ', .true., .false.
    print *, .true., .true..or..true., .true..or..false.
    print *, .false., .false..or..true., .false..or..false.
    print *, '.not.'
    print *, .true., .not..true.
    print *, .false., .not..false.
  
    print *, '.eqv.'
    print *, ' ', .true., .false.
    print *, .true., .true..eqv..true., .true..eqv..false.
    print *, .false., .false..eqv..true., .false..eqv..false.
    print *, '.neqv.'
    print *, ' ', .true., .false.
    print *, .true., .true..neqv..true., .true..neqv..false.
    print *, .false., .false..neqv..true., .false..neqv..false.
end program main
\end{lstlisting}

\subsection{关系运算}

关系运算符有六个: \texttt{<}, \texttt{<=}, \texttt{>}, \texttt{>=}, \texttt{==}, \texttt{/=}.如果运算符左右两边是数, 则六个运算符分别代表$<$, $\leqslant$, $>$, $\geqslant$, $=$, $\ne$, 这种情况下关系运算符经常和逻辑运算符混用. 此时请务必遵循规范\ref{use_barket}, 加上括号来明确究竟哪部分先算.

严格意义上说, 关系运算符是不能连用的. 比如下面这个程序, Gfortran是运行不了的. 然而Ifx是可以的\dots
\begin{lstlisting}
program main
    implicit none
    print *, 1<2<3
end program main
\end{lstlisting}

要做``连续的关系运算'', 标准做法是把``连续的关系运算''拆分成``单独的关系运算'', 然后用\texttt{.and.}连起来, 像下面这样.
\begin{lstlisting}
program main
    implicit none
    print *, (1<2).and.(2<3)
end program main
\end{lstlisting}

这里有一个经典的坑. 像下面这样的程序可能得出``错误''的结果.
\begin{lstlisting}
program main
    implicit none
    print *, (0.1+0.2)==0.3
end program main
\end{lstlisting}
因为实型其实是不能``准确存储''的, 比如对电脑而言, \texttt{0.1}可能不是真的$0.1$, 而是非常接近$0.1$的某个值, \texttt{0.2}和\texttt{0.3}也是这样, 所以\texttt{0.1}加上\texttt{0.2}还真可能不等于\texttt{0.3}. 这个``问题''广泛存在于各种编程语言中, 连Python3默认情形下都是如此. 然而上面的程序Ifx和Gfortran都能得出正确的结果, 似乎非常高级\dots

尽管得出正确结果了, 还是不好说其他情形下会不会出问题. 所以还是要慎重判断实型(和复型)变量是否相等(或不等). 不过整型总是可以随便判断的, 因为整型量的存储是绝对精确的.

如果运算符左右两边是字符串, 则比较运算是比较字符串的先后顺序. 首先, 编译器自己定义了一个字符的先后顺序. 然后比较字符串的先后顺序的时候, 编译器先把两个字符串补成一样长(短的那个后面补空格).
\begin{itemize}
    \item 如果两边的字符串前$n$个字符都相同, 且左边字符串第$(n+1)$个字符先于右边字符串第$(n+1)$个字符, 则左边字符串``\texttt{<}''右边字符串.
    \item 如果两边的字符串前$n$个字符都相同, 且左边字符串第$(n+1)$个字符后于右边字符串第$(n+1)$个字符, 则左边字符串``\texttt{>}''右边字符串.
    \item 如果两边的字符串完全相同, 则左边字符串``\texttt{==}''右边字符串.
\end{itemize}
自然, ``\texttt{>=}''就是不``\texttt{<}'', ``\texttt{<=}''就是不``\texttt{>}'', ``\texttt{/=}''就是不``\texttt{==}''.

注意, 上面``补空格''的一步只是暂时的, 比较前后变量本身没有改变. 比如下面这个程序, 比较前后\texttt{c}都是``\texttt{I}'', 没有变成``\texttt{I  }''\footnote{
否则长度就变成3了, 这不符合``字符串变量长度不变''的规则.
}.
\begin{lstlisting}
program main
    implicit none
    character(1) :: c
    c = 'I'
    print *, c=='You'
    print *, '"', c, '"'
end program main
\end{lstlisting}

至于编译器要怎么定义字符的先后顺序, Fortran标准给了一些规定, 但没规定死, 也就是说不同编译器进行字符串比较可能得出不同的结果. 所以比较字符串的前后顺序不是件好事. 不过一般只会比较字符串是相同还是不同\footnote{
    恭喜同志们, 不必记上面那一大段规则了.
}, 这可以放心比, 合格的编译器得出的结果都相同. 经测试, Ifx和Gfortran在这方面都是合格的. 同志们可以自己研究研究, Ifx和Gfortran的字符先后顺序到底是不是ASCII的顺序.
